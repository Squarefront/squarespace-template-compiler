
import org.apache.tools.ant.filters.ReplaceTokens
import org.gradle.plugins.signing.Sign

buildscript {
  repositories {
    mavenCentral()
    jcenter()
  }

  dependencies {
    classpath 'com.github.townsfolk:gradle-release:1.2'
    classpath 'nl.javadude.gradle.plugins:license-gradle-plugin:0.11.0'
  }
}

task wrapper(type: Wrapper) {
  gradleVersion = '1.12'
}

// Sign release archives
ext.isRelease = !version.endsWith('SNAPSHOT')
ext.isUpload = false
gradle.taskGraph.whenReady { taskGraph ->
  ext.isUpload = gradle.taskGraph.hasTask(':uploadArchives') || gradle.taskGraph.hasTask(':uploadInternal')
  if (isRelease && isUpload && gradle.taskGraph.allTasks.any { it instanceof Sign }) {
    def password = System.console().readPassword('\n\n\n===>  Signing key password: ')
    allprojects { ext.'signing.password' = password }
  }
}

// Disable Javadoc linting under Java 8
if (JavaVersion.current().isJava8Compatible()) {
  allprojects {
    tasks.withType(Javadoc) {
      options.addStringOption('Xdoclint:none', '-quiet')
    }
  }
}

def configurePom(def pom) {
  pom.project {
    name "template-compiler"
    description "Template compiler based on JSON-Template"
    url 'https://github.com/squarespace/squarespace-template'

    licenses {
      license {
        name 'The Apache Software License, Version 2.0'
        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
        distribution 'repo'
      }
    }

    scm {
      connection 'scm:git:git@github.com:squarespace/squarespace-template.git'
      url 'scm:git:git@github.com:squarespace/squarespace-template.git'
      developerConnection 'scm:git:git@github.com:squarespace/squarespace-template.git'
    }

    issueManagement {
      system 'github'
      url 'https://github.com/squarespace/squarespace-template/issues'
    }
  }
}

allprojects {
  apply plugin: 'java'
  apply plugin: 'eclipse'
  apply plugin: 'application'
  apply plugin: 'license'
  apply plugin: 'checkstyle'
  apply plugin: 'jacoco'
  apply plugin: 'signing'
  apply plugin: 'maven'
  apply plugin: 'release'

  group 'com.squarespace.template'

  sourceCompatibility = 1.7
  targetCompatibility = 1.7

  project.ext {
    BUILD_DATE = new java.util.Date()
    BUILD_COMMIT = ('git rev-parse HEAD').execute().text.trim()
  }

  license {
    header rootProject.file('conf/apache-2.0-header.txt')
    ext.year = Calendar.getInstance().get(Calendar.YEAR)
    skipExistingHeaders true
    include '**/*.java'
  }

  repositories {
    mavenCentral()
  }

  dependencies {
    compile 'com.fasterxml.jackson.core:jackson-core:2.3.1'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.3.1'
    compile 'commons-io:commons-io:2.1'
    compile 'joda-time:joda-time:2.3'
    compile 'org.apache.commons:commons-lang3:3.1'
    
    // TODO: move when separating cli into subproject
    compile 'net.sourceforge.argparse4j:argparse4j:0.4.4'
	
    testCompile 'org.testng:testng:6.8.7'
  }

  checkstyle {
    toolVersion = '5.7'
    ignoreFailures = false
    configFile = rootProject.file('checkstyle.xml')
  }

  test {
    useTestNG()
    finalizedBy jacocoTestReport
  }

  jacoco {
    toolVersion '0.7.1.201405082137'
  }

  jacocoTestReport {
    group = 'Reporting'
    additionalSourceDirs = files(sourceSets.main.allJava.srcDirs)
    reports {
      html.destination = "${buildDir}/jacoco-report"
    }
  }

  eclipse.project {
    natures 'net.sf.eclipsecs.core.CheckstyleNature'
    buildCommand 'net.sf.eclipsecs.core.CheckstyleBuilder'
  }

  signing {
    required { isRelease && isUpload }
    sign configurations.archives
  }

  task testJar(type: Jar, dependsOn: testClasses) {
    classifier = 'tests'
    from(
      sourceSets.main.output,
      sourceSets.test.output
    )
  }

  task sourcesJar(type: Jar, dependsOn:classes) { 
    classifier = 'sources' 
    from sourceSets.main.allSource 
  } 

  // Ensure this always runs, to generate fresh build.properties
  processResources.outputs.upToDateWhen { false }

  task javadocJar(type: Jar, dependsOn:javadoc) { 
    classifier = 'javadoc' 
    from javadoc.destinationDir 
  } 

  task execJar(type: Jar, dependsOn: classes) {
    classifier = 'exec'
    manifest {
      attributes 'Main-Class': "${mainClassName}"
    }
    from files(sourceSets.main.output.classesDir)
    from files(sourceSets.main.output.resourcesDir)
    from {
      configurations.runtime.collect {
        it.isDirectory() ? it : zipTree(it)
      }
    }
  }

  artifacts {
    archives sourcesJar
    archives testJar
    archives javadocJar
    archives execJar
  }

  mainClassName = 'com.squarespace.template.cli.TemplateC'

  processResources {
    from (sourceSets.main.resources.srcDirs) {
      include '**/build.properties'
      filter(ReplaceTokens, tokens: [
        'build.version': project.version,
        'build.date': BUILD_DATE.toString(),
        'build.commit': BUILD_COMMIT
      ])
    }

    from (sourceSets.main.resources.srcDirs) {
      exclude '**/build.properties'
    }
  }

  jar {
    manifest {
      attributes 'Main-Class': "${mainClassName}"
    }
  }

  task makeCli(type: Exec, dependsOn: execJar,
    description: 'Creates executable "templatec" command') {

    def inputScript = "src/main/resources/scripts/templatec.in"
    def inputArchive = execJar.archivePath
    workingDir '.'
    executable 'bash'
    args '-c', "cat ${inputScript} ${inputArchive} >templatec; chmod ug+x templatec"
  }

  uploadArchives {
    repositories.mavenDeployer {
      repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
        authentication(
          userName: System.getenv('OSS_USERNAME'),
          password: System.getenv('OSS_PASSWORD')
        )
      }
      
      snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots/') {
        authentication(
          userName: System.getenv('OSS_USERNAME'),
          password: System.getenv('OSS_PASSWORD')
        )
      }

      configurePom(pom)
    }
  }

  if (project.hasProperty('NEXUS_INTERNAL_RELEASE_URL')) {
    task uploadInternal(type: Upload) {
      description = 'Uploads archives to internal Nexus'
      configuration = configurations.archives

      repositories.mavenDeployer {
        repository(url: NEXUS_INTERNAL_RELEASE_URL)
        snapshotRepository(url: NEXUS_INTERNAL_SNAPSHOT_URL)
        configurePom(pom)
      }
    }
  }

}

